///////////////////////////////////////////////////////////////////////////////
//  Entity primary actions                                                   //
///////////////////////////////////////////////////////////////////////////////

=enttype = [|| (=s * $arg1) [=s (enttype) $arg1]]

=entattr = [= (entattr $arg1) $arg2]

// clear ents of given type
clearents = [
    if $editing [
       entcancel
       entselect [=enttype $arg1]
       echo Deleted (enthavesel) $arg1 entities
       delent
    ]
]

selentedit     = [saycommand "/entattr "]
selentfindall  = [do [entfind @(enttype)]]

// modify given attribute of ent by a given amount
// arg1 attribute
// arg2 value
entproperty = [entattr $arg1 (+ (entattr $arg1) $arg2)]

///////////////////////////////////////////////////////////////////////////////
//  Copy & Paste                                                             //
///////////////////////////////////////////////////////////////////////////////

// 3 types of copying and pasting
// 1. select only cubes      -> paste only cubes
// 2. select cubes and ents  -> paste cubes and ents. same relative positions
// 3. select only ents       -> paste last selected ent. if ents are selected, replace attrs as paste

opaquepaste = 1
entcopied   = 0

editcopy = [
    if (|| (havesel) [! (enthavesel)]) [
        entcopied = 0
        entcopy
        copy
    ] [
        entcopied = 1
        ofentcopy
    ]
]

editpaste = [
    cancelpaste = (! (|| (enthavesel) [havesel]));
    if (! $entcopied) [
        pastehilite
        reorient // temp; real fix will be in octaedit
        onrelease [
            if $opaquepaste delcube
            paste; entpaste
            if $cancelpaste [cancelsel]
        ]
    ] [
        save_mouse_position
        ofpasteent
        if $cancelpaste [cancelsel]
    ]
]

///////////////////////////////////////////////////////////////////////////////
//  Selection                                                                //
///////////////////////////////////////////////////////////////////////////////

// select ents with given properties
// '*' is wildcard
entfind = [
    if $numargs [
        entselect (concat [&& [=enttype @@arg1]] (loopconcat* i 2 (div (- $numargs 1) 2) [
            result [[=entattr @@[arg@(+ $i 2)] @@[arg@(+ $i 3)]]]
        ]))
    ] [entselect 1]
]

entfindinsel = [
    if $numargs [
       entselect (concat [&& insel [=enttype @@arg1]] (loopconcat* i 2 (div (- $numargs 1) 2) [
            result [[=entattr @@[arg@(+ $i 2)] @@[arg@(+ $i 3)]]]
        ]))
    ] [entselect insel]
]

drag       = [dragging 1; onrelease [dragging 0]]
corners    = [selectcorners 1; dragging 1; onrelease [selectcorners 0; dragging 0]]
editmove   = [moving 1; onrelease [moving 0]; result $moving]
entdrag    = [entmoving 1; onrelease [finish_dragging; entmoving 0]; result $entmoving]
editdrag   = [cancelsel; || (entdrag) [drag]]
selcorners = [if $hmapedit [hmapselect] [cancelsel; || (entdrag) [corners]]]
editextend = [|| (entdrag) [if (havesel) [selextend; reorient; editmovecut] [drag]]]

editmovewith = [
    if (havesel) [
       || (editmove) [arg1]
       onrelease [moving 0; dragging 0]
    ] [
       arg1
    ]
]

editmovecorner = [editmovewith selcorners]
editmovedrag   = [editmovewith editdrag]


///////////////////////////////////////////////////////////////////////////////
//  Miscellaneous Editing Commands                                           //
///////////////////////////////////////////////////////////////////////////////

editfacewsel = [
    if (|| havesel [! (enthavesel)]) [
        if $moving [pushsel $arg1] [
            entcancel
            editface $arg1 $arg2
        ]
    ]
]

entswithdirection = "marker mapmodel decal"

entdirection = [
    if (&& (enthavesel) [= (havesel) 0]) [
        if (>= (indexof $entswithdirection (enttype)) 0) [
            if (> $arg1 0) [
                entproperty 0 $arg2
                if (> (entattr 0) 360) [entproperty 0 -360]
            ] [
                entproperty 0 (- 0 $arg2)
                if (< (entattr 0) 0) [entproperty 0 360]
            ]
        ]
        result 1
    ] [
        result 0
    ]
]

selmovecut = 1
toggleeditmovecut = [
    selmovecut = (! $selmovecut)
    echo (? $selmovecut "Move Selection" "Cut Selection")
]
editmovecut = [
    if $selmovecut [editmove] [editcut]
]

editdel    = [if (! (enthavesel)) [delcube]; delent]
editflip   = [flip; entflip]
editrotate = [|| (entdirection $arg1 15) [rotate $arg1; entrotate $arg1]]
editcut    = [
    if (moving 1) [
        if (= $moving 1) [selsave]
        onrelease [
            moving 0
            if (selmoved) [
                selswap
                copy; entcopy
                delcube; delent
                selrestore
                paste; entpaste
            ]
        ]
    ]
]

setting_entediting = 1

passthrough = [
    passthroughsel $arg1
    if $arg1 [
        passthroughcube_bak = $passthroughcube
        passthroughcube 1
    ] [
        passthroughcube $passthroughcube_bak
    ]
    entcancel 
    if $setting_entediting [entediting (! $arg1)]
]

enttypelist = [Marker Oriented_Marker Light Spot_Light Envmap Sound Mapmodel Obstacle Decal]
entcomplete = [listcomplete $arg1 $enttypelist]
entcomplete newent
entcomplete entfind
entcomplete clearents

listcomplete editmat "air water clip glass noclip lava gameclip death nogi alpha"
air = [ editmat air $arg1 ]
looplist i ["" 2 3 4] [
    [water@i] = [ editmat water@i $arg1 ]
    [lava@i]  = [ editmat lava@i $arg1 ]
    [glass@i] = [ editmat glass@i $arg1 ]
]
clip     = [editmat clip $arg1]
noclip   = [editmat noclip $arg1]
gameclip = [editmat gameclip $arg1]
death    = [editmat death $arg1]
nogi     = [editmat nogi $arg1]
alpha    = [editmat alpha $arg1]

blendpaintmodes = ["off" "replace" "dig" "fill" "inverted dig" "inverted fill"]

minimaphere = [minimapheight (at (getcampos) 2)]

getsundir = [sunlightyaw (getcamyaw); sunlightpitch (getcampitch)]

defvarp editfloatspeed 1 1000 10000

texturecull = [compactvslots 1]
matpackload = [exec [media/texture/material/@arg1.mat]]
decalpackload = [exec [media/decal/@arg1.dec]]

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
edithud = [
    format "%4^f7%5^f7gridsize :^f2 %1 ^f7| worldsize :^f2 %2 ^f7| floatspeed :^f2 %3" $gridpower $mapscale $floatspeed (
        if $hmapedit [format "heightmap brush^f2 %1 ^f7:^f2 %2^n" $hbrushindex $hbrushname] [
            if $blendpaintmode [
                format "blendpaint mode : ^f2%1^f7^nblend brush : ^f2%2^n" (at $blendpaintmodes $blendpaintmode) (getblendbrushname (curblendbrush))
            ] [if (! (enthavesel)) [
                format "index^f2 %1 ^f7:^f2 %2^n" (max 0 (getseltex)) (gettexname (getseltex))
            ]]
        ]
    ) (if (enthavesel) [concat (entgetinfo) ":^f2" (enthavesel) "^f7selected^n"])
]
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
