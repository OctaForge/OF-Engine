// these default settings get executed whenever "config.cfg" is not available
// do not modify anything below, instead change settings in game, or add to autoexec.cfg

echo "OF defaults"

invmouse 0         // 1 for flightsim mode
sensitivity 3
sensitivityscale 1

musicvol 60
soundvol 255

fov 100
gamma 100
fullbrightmodels 25 // make player models a bit easier to see

// console

consize 5            // console is 5 lines
miniconsize 5        // mini-console is 5 lines
miniconwidth 40      // mini-console is 40% of screen width
fullconsize 75       // full console is 75% of screen height
miniconfilter 0x300  // display chat and team chat in mini-console
confilter (&~ 0x2FFF $miniconfilter) // don't display other player frags or mini-console stuff in console
fullconfilter 0xFFFF // display all messages in full console

// universal scrollwheel + modifier commands:

defaultmodifier = 0
modifier = $defaultmodifier
domodifier = [modifier = $arg1; onrelease [modifier = $defaultmodifier]]
universaldelta = [do [delta_@(if $editing [result edit] [result game])_@modifier @arg1]]
bind MOUSEWHEELUP [ universaldelta 1 ]
bind MOUSEWHEELDOWN [ universaldelta -1 ]

// key binds

bind MOUSELEFT [mouse1click]
bind MOUSERIGHT [mouse2click]
bind MOUSEMIDDLE [mouse3click]

bind W [forward]
bind A [left]
bind S [backward]
bind D [right]

bind UP [forward]
bind DOWN [backward]
bind RIGHT [right]
bind LEFT [left]

bind SPACE [jump]
bind LSHIFT [crouch]
bind LCTRL [crouch]

bind BACKQUOTE [saycommand /]
bind SLASH [saycommand /]
bind KP_MINUS [conskip 5]
bind KP_PLUS [conskip -1000]
bindvarquiet PAUSE [paused]

bind F6  [toggleui loadmap]
bind F11 [toggleconsole]
bind F12 [screenshot]

bind E [edittoggle]
bind T [saycommand]
bind Y [sayteamcommand]
bind Z [togglezoom]

delta_game_0 = [if (= $fullconsole 1) [if (= $arg1 1) [conskip 3] [conskip -3]] []]

bind M [if (= $freecursor 1) [freecursor 2] [freecursor 1] ]
editbind M [if (= $freeeditcursor 1) [freeeditcursor 2] [freeeditcursor 1] ]

// edit binds

editbind MOUSELEFT   [if $blendpaintmode [paintblendmap] [editdrag]]
editbind MOUSEMIDDLE [selcorners]
editbind MOUSERIGHT  [
    if (= $has_mouse_target 0) [
        if $blendpaintmode [rotateblendbrush] [editextend]
    ] [
        show_entity_ui
    ]
]

editbind SPACE    [cancelsel]
editbind COMMA    [domodifier 8] // vSlot: rotate
editbind PERIOD   [domodifier 9] // vSlot: scale
editbind LSHIFT   [floatspeed 1000;onrelease [floatspeed 100]]
editbindmod LCTRL [passthrough]
editbind DELETE   [editdel]
editbind HOME     [gotosel]
editbind END      [domodifier 15; onrelease entautoview] // Hold and scroll to goto selected entities

editbind 1    [domodifier 10] // Entity attribute modifier 0
editbind 2    [domodifier 11] // Entity attribute modifier 1
editbind 3    [domodifier 12] // Entity attribute modifier 2
editbind 4    [domodifier 13] // Entity attribute modifier 3
editbind 5    [domodifier 14] // Entity attribute modifier 4
editbindvar 6 [allfaces]
editbindvar 7 [entselsnap]
editbindvar 8 [entediting]
editbind 9    [selentedit]
editbind 0    [toggleeditmovecut] // Toggle MOUSERIGHT to move selection with or without contents
editbindvar MINUS  [outline]
editbindvar EQUALS [wireframe]

// for reference, not functional yet
editbind F1  [toggleui entity]              // Add/Edit Entity
editbind F2  [toggleui texture]             // Texture List
editbind F3  [toggleui mapmodel]            // Add Mapmodel
editbind F4  [toggleui material]            // Edit Material
editbind F5  [toggleui savemap]             // Savemap
editbind F7  [toggleui env_settings]        // Environment Settings
editbind F8  [toggleui geo_prefab]          // World Geometry Prefab
editbind F9  [toggleui misc_editing]        // Miscellaneous Editing
editbind F10 [toggleui editbind_reference]  // Default Editbind Quick Reference

editbind KP0 [setblendpaintmode 0]  // Exit paint blendmap mode
editbind KP1 [setblendpaintmode 1]
editbind KP2 [setblendpaintmode 2]  // Paint
editbind KP3 [setblendpaintmode 3]
editbind KP4 [setblendpaintmode 4]
editbind KP5 [setblendpaintmode 5]  // Erase
editbind KP8 [scrollblendbrush -1]  // Select brush -
editbind KP9 [scrollblendbrush  1]  // Select brush +
editbind KP_ENTER [entselect insel] // Select entities within area selection

editbind B [domodifier 5]  // Change heightmap brush
editbind C [editcopy]
editbind F [domodifier 2]  // Push face/corners selected
editbind G [domodifier 1]  // Grid size
editbind H [if $hmapedit [editface 1 -1] [hmapedit 1]]
editbind I [redo]
editbind J [gotosel]
editbind K [calclight]
editbindvar L [fullbright]
editbindvar M [showmat]
editbind N [selentfindall]
editbind O [domodifier 6]  // vSlot: offset H
editbind P [domodifier 7]  // vSlot: offset V
editbind Q [domodifier 16] // Push corner pointed at by cursor
editbind R [domodifier 3]  // Geometry rotate -or- blend brush rotate
editbind U [undo; passthroughsel 0]
editbind V [editpaste]
editbind X [editflip]      // Geometry flip
editbind Y [domodifier 4]  // Change textures
editbind Z [history 0]     // Repeat last saycommand

delta_edit_0  = [if (= $fullconsole 1) [if (= $arg1 1) [ conskip 3 ] [ conskip -3 ]] [if $blendpaintmode [scrollblendbrush $arg1] [editfacewsel $arg1 1]]]  // Console scroll -or- Change blend brush -or- Push/pull geometry
delta_edit_1  = [nodebug [gridpower (+ $arg1 $gridpower)]]                       // Grid size
delta_edit_2  = [editfacewsel $arg1 0]                                           // Push face/corners selected
delta_edit_16 = [editfacewsel $arg1 2]                                           // Push corner pointed at by cursor
delta_edit_3  = [if $blendpaintmode [rotateblendbrush $arg1] [editrotate $arg1]] // Geometry rotate -or- blend brush rotate
delta_edit_4  = [edittex $arg1]                                                  // Change textures
delta_edit_5  = [selecthbrush $arg1]                                             // Change heightmap brush
delta_edit_6  = [vdelta [voffset (* $arg1 $multiplier2) 0]]                      // vSlot: offset H
delta_edit_7  = [vdelta [voffset 0 (* $arg1 $multiplier2)]]                      // vSlot: offset V
delta_edit_8  = [vdelta [vrotate $arg1]]                                         // vSlot: rotate
delta_edit_9  = [vdelta [vscale (if (< $arg1 0) 0.5 2)]]                         // vSlot: scale
delta_edit_10 = [entproperty 0 (* $arg1 $multiplier1)]
delta_edit_11 = [entproperty 1 (* $arg1 $multiplier1)]
delta_edit_12 = [entproperty 2 (* $arg1 $multiplier1)]
delta_edit_13 = [entproperty 3 (* $arg1 $multiplier1)]
delta_edit_14 = [entproperty 4 (* $arg1 $multiplier1)]
delta_edit_15 = [entautoview $arg1]

editbind LALT [multiplier1 = 10;onrelease [multiplier1 = 1]]  // Entity attribute multiplier
editbind RALT [multiplier2 = 32;onrelease [multiplier2 = 16]] // vSlot: offset multiplier
multiplier1 = 1
multiplier2 = 16
