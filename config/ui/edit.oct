import capi
import core.engine.edit
import core.engine.input
import core.entities.ents
import core.entities.svars
import core.events.signal
import core.gui.core as gui

from std.table import pairs
from std.conv import tostring

var root = gui::get_root()

var slots_populate = func(gr, slotlist, isv) {
    gr.destroy_children()
    for i, slot in slotlist.each() {
        var ttip = gui::Outline({ variant: "normal" },
            gui::ColorFiller({ color: 0x151515, min_w: 0.225, min_h: 0.225 },
            (isv ? gui.VSlotViewer : gui.SlotViewer)({
                index: slot, min_w: 0.22, min_h: 0.22
            })
        ))
        var vb = gr.append(gui::VBox())
        var btn = vb.append(gui::Button({ variant: isv ? "vslot" : "slot",
            index: slot, min_w: 0.095, min_h: 0.095
        }), func(b) {
            signal::connect(b, "clicked", \-> capi::slot_set(
                isv ? slot : capi::slot_get_tex(slot)))
            signal::connect(b, "hovering", \self -> self.show_tooltip(ttip))
        })
        var lbl = vb.append(gui::Label({
            tex: "0x0", scale: 0.7, align_h: -1, color: 0x50FFFFFF
        }))
        signal::connect(btn, "tex_size,changed", func(o, tw, th) {
            lbl.set_text(tw ~ "x" ~ th)
        })
    }
}

var last_filter

var gen_slot_filters = func(packs, gr) {
    var pcbs, pcbl = {}, []

    pcbs["all"] = func() {
        var slotl = []
        for i in 0 to capi::slot_get_count() - 1 {
            slotl.push(i)
        }
        slots_populate(gr, slotl)
        last_filter = "all"
    }
    pcbl.push("all")

    var otherslots = []
    for i in 0 to capi::slot_get_count() - 1 {
        otherslots.push(i)
    }

    for i, val in packs.each() {
        var slotl = []
        for i in val[1] to val[1] + val[2] - 1 {
            otherslots[i] = undef
            slotl.push(i)
        }
        var fcb = func() {
            slots_populate(gr, slotl)
            last_filter = val[0]
        }
        pcbs[val[0]] = fcb
        pcbl.push(val[0])
    }

    otherslots.compact(\v -> v)
    if otherslots.len() > 0 {
        var ocb = func() {
            slots_populate(gr, otherslots)
            last_filter = "other"
        }
        pcbs["other"] = ocb
        pcbl.push("other")
    }

    return pcbs, pcbl
}

var gen_tex_layout = func(bx, gr, grid_load) {
    var s
    bx.append(gui::Outline({ variant: "normal", min_w: 0.84, min_h: 0.6 }), func(o) {
        o.append(gui::Spacer({ pad_h: 0.005, pad_v: 0.005 }), func(sp) {
            sp.align(-1, -1)
            sp.append(gui::Scroller({ clip_w: 0.9, clip_h: 0.6 }), func(sc) {
                sc.append(gr, grid_load)
                s = sc
            })
        })
    })
    bx.append(gui::VScrollbar({ clamp_v: true }), func(sb) {
        sb.append(gui::ScrollButton())
        sb.bind_scroller(s)
    })
}

var gen_tex_listed = func() {
    var r = gui::VBox()

    var gr = gui::Grid({ columns: 8, padding: 0.01 })
    var sp = r.append(gui::Spacer({ pad_h: 0.005, pad_v: 0.005 }))
    var hb = sp.append(gui::HBox({ padding: 0.02 }))
    hb.append(gui::Label({ text: "Filter by pack:" }))

    var cbx = hb.append(gui::MenuButton({ variant: "combobox", min_w: 0.25 }))
    var mnc = gui::Filler({ variant: "menu", min_w: 0.25 })
    mnc.parent = cbx
    var mn = mnc.append(gui::VBox({ clamp: true }))
    signal::connect(cbx, "clicked", func(mb) {
        mb.show_menu(mnc)
    })

    var grid_load = func() {
        var pcbs, pcbl = gen_slot_filters(capi::texture_get_packs(), gr)
        mn.destroy_children()
        for i, pname in pcbl.each() {
            var mb = mn.append(gui::MenuButton({ variant: "submenu",
                label: pname, clamp_h: true }))
            signal::connect(mb, "clicked", func() {
                pcbs[pname]()
                cbx.set_label(pname)
                root.clear_menus()
            })
        }
        cbx.set_disabled(pcbl.len() == 0)
        var cb
        if last_filter { cb = pcbs[last_filter] }
        if !cb {
            cb = pcbs["all"]
            last_filter = undef
        }
        cbx.set_label(last_filter || "all")
        cb()
    }
    var cid = signal::connect(edit, "texpacks,changed", grid_load)
    signal::connect(r, "destroy", func() {
        signal::disconnect(edit, "texpacks,changed", cid)
    })

    var hb = r.append(gui::HBox())
    gen_tex_layout(hb, gr, grid_load)
    return r
}

var gen_tex_utilized = func() {
    var hb = gui::HBox()

    var gr = gui::Grid({ columns: 8, padding: 0.01 })
    var grid_load = func() {
        var slotl = []
        for i in 0 to capi::slot_texmru_num() - 1 {
            slotl.push(capi::slot_texmru(i))
        }
        slots_populate(gr, slotl, true)
    }
    var cid = signal::connect(edit, "texpacks,changed", grid_load)
    signal::connect(hb, "destroy", func() {
        signal::disconnect(edit, "texpacks,changed", cid)
    })

    gen_tex_layout(hb, gr, grid_load)
    return hb
}

var laststate
root.new_window("texture", gui.Window, func(win) {
    win.set_floating(true)
    win.set_variant("movable")
    var ttl = gui::HBox({ padding: 0.005 })
    var stat
    var lbtn = ttl.append(gui::Button({ label: "Listed", min_w: 0.14 }))
    var ubtn = ttl.append(gui::Button({ label: "Utilized", min_w: 0.14 }))
    signal::connect(lbtn, "clicked", \-> stat.set_state("listed"))
    signal::connect(ubtn, "clicked", \-> stat.set_state("utilized"))
    win.set_title(ttl)
    win.append(gui::State({ state: laststate || "utilized" }), func(st) {
        stat = st
        st.update_state("listed"  , gen_tex_listed())
        st.update_state("utilized", gen_tex_utilized())
        signal::connect(st, "state,changed", func(self, nstate) {
            laststate = nstate
            lbtn.set_variant((nstate == "listed") ? "default" : "nobg")
            ubtn.set_variant((nstate != "listed") ? "default" : "nobg")
        })
        signal::emit(st, "state,changed", st.state)
    })
})

var fields = {
    (svars.StateBoolean): func(hb, nm, ent, dv) {
        var tvar = (dv == "true")
        var ret
        hb.append(gui::Filler({ min_w: 0.4 }), func(f) {
            f.append(gui::Toggle({ variant: "checkbox", condition: \-> tvar,
                align_h: -1
            }), func(t) {
                ret = t
                signal::connect(t, "released", func() {
                    tvar = !tvar
                    capi::makeundoent()
                    ent.set_gui_attr(nm, tostring(tvar))
                })
            })
        })
        return ret
    }
}
var field_def = func(hb, nm, ent, dv) {
    return hb.append(gui::Field({ clip_w: 0.4, value: dv }), func(ed) {
        signal::connect(ed, "value,changed", func(ed, v) {
            capi::makeundoent()
            ent.set_gui_attr(nm, v)
        })
    })
}

root.new_window("entity", gui.Window, func(win) {
    win.set_floating(true)
    win.set_variant("movable")
    var  ent = capi::get_selected_entity()
    if !ent {
        ent = ents::get_player()
    }
    if !ent {
        win.set_title("Entity editing: none")
        win.append(gui::Spacer({ pad_h: 0.04, pad_v: 0.03 },
            gui::Label({ text: "No selected entity" })))
        return
    }
    win.set_title("Entity editing: %s (%d)".format(ent.name, ent.uid))
    var props = []
    var sdata = []
    var sdata_raw = ent.build_sdata()

    var prefix = "_SV_"
    for k, v in pairs(sdata_raw) {
        var sv = ent[prefix ~ k]
        var gn = sv.gui_name
        if gn != false {
            sdata[k] = [ gn || k, sv.to_data(v), sv ]
            props.push(k)
        }
    }
    props.sort()

    win.append(gui::HBox(), func(hb) {
        var s
        hb.append(gui::Outline({ variant: "normal" }), func(o) {
            o.append(gui::Spacer({ pad_h: 0.005, pad_v: 0.005 }), func(sp) {
                sp.append(gui::Scroller({ clip_w: 0.9, clip_h: 0.6 }), func(sc) {
                    sc.append(gui::VBox(), func(vb) {
                        var fpf, pf
                        for i, nm in props.each() {
                            var sd = sdata[nm]
                            var gn, dv, sv = sd[0], sd[1], sd[2]
                            vb.append(gui::HBox({ align_h: 1 }), func(hb) {
                                hb.append(gui::Label({ text: " " ~ sd[0]
                                    ~ ": " }))
                                var fld = fields[sv.__proto] || field_def
                                var fd = fld(hb, gn, ent, dv)
                                if pf { pf.set_tab_next(fd) }
                                pf = fd
                                if !fpf { fpf = fd }
                            })
                            if fpf && pf && pf != fpf {
                                pf.set_tab_next(fpf)
                            }
                        }
                    })
                    s = sc
                })
            })
        })
        hb.append(gui::VScrollbar({ clamp_v: true }), func(sb) {
            sb.append(gui::ScrollButton())
            sb.bind_scroller(s)
        })
    })
})

root.new_window("entity_new", gui.Window, func(win) {
    win.set_floating(true)
    win.set_variant("movable")
    win.set_title("New entity")

    var cnames = []
    for k, v in pairs(ents::get_all_prototypes()) {
        if v.is_a(ents.StaticEntity) {
            cnames.push(k)
        }
    }
    cnames.sort()

    win.append(gui::HBox(), func(hb) {
        var s
        hb.append(gui::Outline({ variant: "normal" }), func(o) {
            o.append(gui::Spacer({ pad_h: 0.005, pad_v: 0.005 }), func(sp) {
                sp.append(gui::Scroller({ clip_w: 0.6, clip_h: 0.6 }), func(sc) {
                    sc.append(gui::VBox(), func(vb) {
                        for i, n in cnames.each() {
                            vb.append(gui::Button({
                                variant: "nobg", min_w: 0.3, label: n
                            }), func(btn) {
                                signal::connect(btn, "clicked", func() {
                                    edit::new_entity(n)
                                    root.hide_window("entity_new")
                                })
                            })
                        }
                    })
                    s = sc
                })
            })
        })
        hb.append(gui::VScrollbar({ clamp_v: true }), func(sb) {
            sb.append(gui::ScrollButton())
            sb.bind_scroller(s)
        })
    })
})
