/**<
    Implements "state variables". State variables are basically entity
    properties. They mimick real property behavior. They automatically
    sync changes between clients/server as required. They can be of
    various types and new svar types are easily implementable.

    Author:
        q66 <daniel@octaforge.org>

    License:
        See COPYING.txt.
*/

import capi

import core.logger as logging

var log = logging.log
var DEBUG = logging.DEBUG
var INFO  = logging.INFO

import core.events.frame
import core.octascript.geom as ogeom
import core.network.msg

from std.object import Object
from std.table import rawget, rawset
from std.math import abs, round, floor
from std.conv import tonumber, tostring
from std.util import assert

func define_accessors(cl, n, gf, sf, d) {
    cl["__get_" ~ n] = func(self) {
        return gf(self, d)
    }
    cl["__set_" ~ n] = func(self, v) {
        return sf(self, v, d)
    }
}

/**
    Provides a base object for a state variable. Specialized svar types
    clone this and define their own methods. Fields can be initialized via
    kwargs.

    Fields:
        - client_read [true] - clients can read the value.
        - client_write [true] - the value can be written clientside
          (indirectly through a server message).
        - client_set [false] - the value can be written clientside
          directly without a server message.
        - client_private [false] - the value will be private to the client,
          other clients won't see it (but the server will).
        - custom_sync [false] - the state variable will use a custom sync
          method (useful for Cube 2 dynents).
        - gui_name [undef] - the name shown in the editing GUI for the svar.
          Can be set to false to hide it from the editing GUI completely.
        - alt_name [undef] - an alternative accessor name.
        - reliable [true] - the messages sent for this svar will be reliable,
          that is, always sent; you cannot send a big number of them. For
          e.g. position updates, you're better off with unreliable messages
          that do not need to be sent all the time.
        - has_history [true] - the var will retain its state and e.g.
          when a new client requests it, it'll receive the values set
          previously (even if set long before the connection).
        - getter_fun [undef] - provided in kwargs as just "getter", a custom
          getter for the state var, used typically with C functions (to handle
          C-side entity changes), takes one argument, an entity this state
          var belongs to.
        - setter_fun [undef] - provided in kwargs as just "setter", a custom
          setter similar to getter. Takes two arguments, the entity and the
          value we're setting. Note that even with getter and setter functions
          the value will be cached for better performance (so we don't always
          have to query).
*/
export var StateVariable = Object.clone({
    name: "StateVariable",

    /// Makes svar objects return their name on tostring.
    __tostring: func(self) {
        return self.name
    },

    /// Initializes the svar. Parameters are passed in kwargs (a dict).
    __ctor: func(self, kwargs) {
        @[debug] log(INFO, "StateVariable: init")

        kwargs = kwargs || {}

        self.client_read    = (kwargs.client_read  != undef) ? kwargs.client_read  : true
        self.client_write   = (kwargs.client_write != undef) ? kwargs.client_write : true
        self.client_set     = kwargs.client_set     || false
        self.client_private = kwargs.client_private || false
        self.client_cb      = kwargs.client_cb      || false

        self.custom_sync = kwargs.custom_sync || false

        self.gui_name, self.alt_name = kwargs.gui_name, kwargs.alt_name

        self.reliable    = (kwargs.reliable    != undef) ? kwargs.reliable    : true
        self.has_history = (kwargs.has_history != undef) ? kwargs.has_history : true

        self.getter_fun = kwargs.getter
        self.setter_fun = kwargs.setter
    },

    /**
        Registers the state variable, given an entity prototype. It'll create
        getter and setter methods on the entity prototype for the given name
        and also for `alt_name` if set in constructor kwargs. You can access
        the raw state variable on the entity prototype by prefixing it with
        `_SV`. You can access the variable by gui_name by prefixing it with
        `_SV_GUI_` (if `gui_name` is not defined, regular name is used, if
        `gui_name` is false, this field won't exist at all).

        Arguments:
            - name - the state var name.
            - cl - the entity prototype.
    */
    register: func(self, name, cl) {
        @[debug] log(DEBUG, "StateVariable: register(" ~ name
            ~ ", " ~ cl.name ~ ")")

        self.name = name
        cl["_SV_" ~ name] = self

        assert(self.getter)
        assert(self.setter)

        @[debug] log(DEBUG, "StateVariable: register: getter/setter")
        define_accessors(cl, name, self.getter, self.setter, self)

        var an = self.alt_name
        if an {
            @[debug] log(DEBUG, "StateVariable: register: alt g/s")
            cl["_SV_" ~ an] = self
            define_accessors(cl, an, self.getter, self.setter, self)
        }
        var gn = self.gui_name
        if gn != false {
            cl["_SV_GUI_" ~ (gn || name)] = self
        }
    },

    /**
        Performs clientside svar read tests. On the server we can always
        read, on the client we can't if client_read is false. Fails an
        assertion if on the client and client_read is false.
    */
    read_tests: func(self, ent) {
        if msg::is_server() { return }
        if !self.client_read {
            raise e"read tests for svar $(self.name) failed"
        }
    },

    /**
        Performs clientside svar write tests. On the server we can always
        write, on the client we can't if client_write is false. Fails an
        assertion if on the client and client_read is false (or if an
        entity is deactivated/uninitialized).
    */
    write_tests: func(self, ent) {
        if ent.deactivated {
            raise e"writing a field $(self.name) of a deactivated entity "
                  e"$(ent.name) ($(ent.uid))"
        }
        var inited = ent.initialized
        if inited && (msg::is_server() || self.client_write) { return }
        raise e"write tests for svar $(self.name) failed"
    },

    /**
        Default getter for a state variable. Works on an entity (which
        is self here). It mostly simply returns the value from an internal
        table. It performs read tests.

        Note that if custom getter function is provided in the constructor's
        kwargs and no sufficient value is cached, it'll return the value
        the getter function returns (and it'll also save into the cache
        for further use).

        Arguments:
            - self - not the state var, it's an entity.
            - svar - the state var.
    */
    getter: func(self, svar) {
        svar.read_tests(self)

        var vn = svar.name
        @[debug] log(INFO, "StateVariable: getter: " ~ vn)

        var fr = frame::get_frame()

        var srv = svar.client_cb && msg::is_server_only()
        if srv || !svar.getter_fun || self.svar_value_timestamps[vn] == fr {
            return self.svar_values[vn]
        }

        @[debug] log(INFO, "StateVariable: getter: getter function")

        var val = self.__sdata_call_cb_get(svar.getter_fun)
        self.svar_values[vn] = val
        self.svar_value_timestamps[vn] = fr

        return val
    },

    /**
        Default setter for a state variable. It simply sets state data.

        Arguments:
            - self - not the state var, it's an entity.
            - val - the value.
            - svar - the state var.
    */
    setter: func(self, val, svar) {
        svar.write_tests(self)
        self.set_sdata(svar.name, val, -1)
    },

    /**
        Validates a state variable value. The default simply returns
        true. Can be overriden.
    */
    validate: func(self, val) { return true },

    /**
        Checks whether changes of this variable should be synced with other
        clients. Returns true if this variable is not client_private or if the
        target client number equals the client number of the given entity.

        Arguments:
            - ent - the entity.
            - tcn - target client number.
    */
    should_send: func(self, ent, tcn) {
        return (!self.client_private) || (ent.cn == tcn)
    },

    /**
        Converts the given value to data format for this state variable.
        It's a string that is user readable. By default it simply converts
        the value to a string.
    */
    to_data: func(self, val) {
        return tostring(val)
    },

    /**
        Converts the given value in data format back to the original
        format. See $to_data. By default simply returns a string.
    */
    from_data: func(self, val) {
        return tostring(val)
    },

    /**
        Converts the given value to an easily serializable format (i.e.
        a format that can be serialized with builtin language types).
        It must be possible to set the result as state data value.

        By default this simply returns the input value (as most state
        variable types represent primitives) but can be overridden
        for more complex types.
    */
    to_serializable: func(self, val) {
        return tostring(val) || ""
    }
})

/// Checks whether the given value is a state variable.
export func is_svar(v) {
    return (typeof v == "table" && v.is_a) && v.is_a(StateVariable)
}

/**
    Specialization of $StateVariable for integer values.
*/
export var StateInteger = StateVariable.clone({
    name: "StateInteger",

    to_data  : func(self, val) { return tostring(val) },
    from_data: func(self, val) { return floor(tonumber(val)) },

    to_serializable: func(self, val) { return floor(tonumber(val)) || 0 }
})

/**
    Specialization of $StateVariable for float values.
*/
export var StateFloat = StateVariable.clone({
    name: "StateFloat",

    to_data  : func(self, val) { return tostring(round(val, 2)) },
    from_data: func(self, val) { return tonumber(val) },

    to_serializable: func(self, val) { return tonumber(val) || 0 }
})

/**
    Specialization of $StateVariable for boolean values.
*/
export var StateBoolean = StateVariable.clone({
    name: "StateBoolean",

    to_data  : func(self, val) { return tostring(val) },
    from_data: func(self, val) { return val == "true" && true || false },

    to_serializable: func(self, val) { return !!val }
})

from std.conv import serialize as ts, deserialize as td

/**
    Specialization of $StateVariable for table values.
*/
export var StateTable = StateVariable.clone({
    name: "StateTable",

    to_data  : func(self, val) { return ts(val) },
    from_data: func(self, val) { return td(val) },

    to_serializable: func(self, val) {
        if typeof val == "table" {
            return val
        }
        return {}
    }
})

/**
    Specialization of $StateVariable for string values.
*/
export var StateString = StateVariable.clone({
    name: "StateString"
})

from std.table import getmt, setmt
from std.util import proxy as newproxy

import std.array

var ArraySurrogate

/**
    Represents a "surrogate" for an array. Behaves like a regular
    array, but does not actually contain anything; it merely serves
    as an interface for state variables like $StateArray.

    You can manipulate this like a regular array (check its length,
    index it, assign indexes) but many of the functions from the
    table library likely won't work.

    Note that surrogates are not regular objects created using the
    prototypal system. They're manually managed with metatables and
    proxies in order to more closely emulate the appropriate semantics.
*/
ArraySurrogate = {
    name: "ArraySurrogate",

    /**
        Constructs the array surrogate. Defines its members "entity"
        and "variable", assigned using the provided arguments.
    */
    new: func(self, ent, svar) {
        @[debug] log(INFO, "ArraySurrogate: new: " ~ svar.name)
        var rawt = { entity: ent, variable: svar }
        rawt.rawt = rawt // yay!cycles!
        var ret = newproxy(true)
        var mt  = getmt(ret)
        mt.__tostring = self.__tostring
        mt.__index    = setmt(rawt, self)
        mt.__newindex = self.__newindex
        return ret
    },

    /// Makes surrogate objects return their names on tostring.
    __tostring: func(self) {
        return self.name
    },

    /**
        Called each time you index an array surrogate. It checks
        the validity of the given index by converting it to a number
        and flooring it. On invalid indexes, it simply fallbacks to
        regular indexing.
    */
    __index: func(self, name) {
        if name == "__size" {
            return self.len()
        }
        var n = tonumber(name)
        if !n {
            return ArraySurrogate[name] || rawget(self.rawt, name)
        }
        var i = floor(n)
        if i != n {
            return ArraySurrogate[name] || rawget(self.rawt, name)
        }

        var v = self.variable
        return v.get_item(self.entity, i)
    },

    /**
        Called each time you set an index on an array surrogate. It checks
        the validity of the given index by converting it to a number and
        flooring it. If the given index is not an integer, this fallbacks
        to regular setting. Otherwise sets the corresponding element using
        the state variable.
    */
    __newindex: func(self, name, val) {
        var n = tonumber(name)
        if !n { return rawset(self.rawt, name, val) }
        var i = floor(n)
        if i != n { return rawset(self.rawt, name, val) }

        var v = self.variable
        v.set_item(self.entity, i, val)
    },

    /// Returns the length of the "array" represented by the state variable.
    len: func(self) {
        var v = self.variable
        return v.get_length(self.entity)
    },

    /// Returns a raw array of values stored using the state variable.
    to_array: func(self) {
        var l = self.len()
        var r = []
        for i in 0 to l - 1 {
            r.push(self[i])
        }
        return r
    },

    /// Appends an element. For convenience only.
    push: func(self, v) {
        self.variable.push_item(self.entity, v)
    },

    map: array.map,
    merge: array.merge,
    filter: array.filter,
    find: array.find,
    find_r: array.find_r,
    foldr: array.foldr,
    foldl: array.foldl,
    each: array.each,
    each_r: array.each_r,
    first: array.first,
    last: array.last,
    empty: array.empty
}

export ArraySurrogate

/**
    Specialization of <StateVariable> for arrays. Uses $ArraySurrogate
    to provide an array-like "interface". The surrogate is required to
    properly reflect array element changes.
*/
export var StateArray = StateVariable.clone({
    name: "StateArray",

    /**
        Specifies the surrogate used by the state variable. By default
        it's $ArraySurrogate, but may be overriden.
    */
    surrogate: ArraySurrogate,

    /**
        Instead of returning the raw value in non-data format, this
        overriden getter returns the appropriate $surrogate. It
        does not create a new surrogate each time; it's cached
        for performance reasons. Performs read tests.

        See also:
            - {{$StateVariable.getter}}
    */
    getter: func(self, svar) {
        svar.read_tests(self)

        if !svar.get_raw(self) { return undef }

        var n = "__as_" ~ svar.name
        if !self[n] { self[n] = svar.surrogate.new(self, svar) }
        return self[n]
    },

    /**
        Works the same as the default setter, but if a surrogate is
        given, then it converts it to a raw array and if a table
        is given, it copies the table before setting it.

        See also:
            - {{$StateVariable.setter}}
    */
    setter: func(self, val, svar) {
        @[debug] log(INFO, "StateArray: setter: " ~ tostring(val))
        svar.write_tests(self)

        self.set_sdata(svar.name,
            val.to_array && val.to_array() || val.copy(), -1)
    },

    /** Function: to_data_item
        This is not a regular method, it has no self. It's called by
        $to_data for each value of the array before including it in
        the result.
    */
    to_data_item: tostring,

    /** Function: from_data_item
        This is not a regular method, it has no self. It's called by
        $from_data for each value of the array before including it in
        the result.
    */
    from_data_item: tostring,

    /**
        Returns the contents of the state array in a data format. The value
        can be either an array or an array surrogate.
    */
    to_data: func(self, val) {
        return ts(self.to_serializable(val).map(
            \v -> self::from_data_item(self::to_data_item(v))))
    },

    /// Converts a string in a format given by $to_data back to a table.
    from_data: func(self, val) {
        var v = td(val)
        if typeof v != "array" {
            return []
        }
        return v.map(self.from_data_item)
    },

    /// Overridden to return a plain array in all cases.
    to_serializable: func(self, val) {
        if typeof val == "array" {
            return val
        }
        if val.to_array {
            return val.to_array()
        }
        return []
    },

    /**
        Returns the raw array of state data. Retrieved from var storage
        without syncing assuming there is either no existing getter function
        or a sufficient cached value. Otherwise returns the result of a
        getter function call and caches it.
    */
    get_raw: func(self, ent) {
        var vn = self.name
        @[debug] log(INFO, "StateArray: get_raw: " ~ vn)

        var srv = self.client_cb && msg::is_server_only()
        if srv || !self.getter_fun {
            return ent.svar_values[vn] || []
        }

        var fr = frame::get_frame()

        if ent.svar_value_timestamps[vn] == fr {
            return ent.svar_values[vn]
        }

        @[debug] log(INFO, "StateArray: get_raw: getter function")

        var val = ent.__sdata_call_cb_get(self.getter_fun)
        ent.svar_values[vn] = val
        ent.svar_value_timestamps[vn] = fr

        return val
    },

    /// Retrieves the state array length. Used by the surrogate.
    get_length: func(self, ent) {
        return self.get_raw(ent).len()
    },

    /**
        Retrieves a specific element from the state array. Used by
        the surrogate.
    */
    get_item: func(self, ent, idx) {
        @[debug] log(INFO, "StateArray: get_item: " ~ idx)
        return self.get_raw(ent)[idx]
    },

    /**
        Sets an element in the state array. Used by the surrogate. Performs
        an update on all clients by setting the state data on the entity.
    */
    set_item: func(self, ent, idx, val) {
        @[debug] log(INFO, "StateArray: set_item: " ~ idx ~ ", "
            ~ tostring(val))
        var a = self.get_raw(ent)
        a[idx] = val
        ent.set_sdata(self.name, a, -1)
    },

    push_item: func(self, ent, val) {
        @[debug] log(INFO, "StateArray: push: " ~ tostring(val))
        var a = self.get_raw(ent)
        a.push(val)
        ent.set_sdata(self.name, a, -1)
    }
})

/**
    A variant of $StateArray for integer contents.
*/
export var StateArrayInteger = StateArray.clone({
    name: "StateArrayInteger",

    to_data_item  : tostring,
    from_data_item: func(v) { return floor(tonumber(v)) }
})

/**
    A variant of $StateArray for floating point contents.
*/
export var StateArrayFloat = StateArray.clone({
    name: "StateArrayFloat",

    to_data_item  : func(v) { return tostring(round(v, 2)) },
    from_data_item: tonumber
})

/**
    A specialization of StateArrayFloat, providing its own surrogate,
    {{$ogeom.Vec2_Surrogate}}. Other than that, no changes are made.
*/
export var StateVec2 = StateArrayFloat.clone({
    name: "StateVec2",
    surrogate: ogeom.Vec2_Surrogate
})

/**
    A specialization of StateArrayFloat, providing its own surrogate,
    {{$ogeom.Vec3_Surrogate}}. Other than that, no changes are made.
*/
export var StateVec3 = StateArrayFloat.clone({
    name: "StateVec3",
    surrogate: ogeom.Vec3_Surrogate
})

/**
    A specialization of StateArrayFloat, providing its own surrogate,
    {{$ogeom.Vec4_Surrogate}}. Other than that, no changes are made.
*/
export var StateVec4 = StateArrayFloat.clone({
    name: "StateVec4",
    surrogate: ogeom.Vec4_Surrogate
})

/**
    Aliases a state variable. Aliases are always registered last so that
    the variables they alias are already registered. They provide alternative
    getters and setters.
*/
export var StateVariableAlias = StateVariable.clone({
    name: "StateVariableAlias",

    /**
        Variable aliases don't really need all the properties, so the parent
        constructor is never called. They have one property, target_name,
        given by the constructor argument, which specifies the name of
        the state variable they point to.
    */
    __ctor: func(self, tname) {
        self.target_name = tname
    },

    /**
        Overriden registration function. It simply sets up the alias
        getter and setter. It also creates the _SV_ prefixed raw accessor
        pointing to the target var. See {{$StateVariable.register}}.
    */
    register: func(self, name, cl) {
        @[debug] log(DEBUG, "StateVariableAlias: register(" ~ name
            ~ ", " ~ cl.name ~ ")")

        self.name = name
        var tg = cl["_SV_" ~ self.target_name]
        cl["_SV_" ~ name] = tg

        @[debug] log(DEBUG, "StateVariableAlias: register: getter/setter")
        define_accessors(cl, name, self.getter, self.setter, self)
    }
})

/// Checks whether the given value is a state variable alias.
export func is_svar_alias(v) {
    return (typeof v == "table" && v.is_a) && v.is_a(StateVariableAlias)
}