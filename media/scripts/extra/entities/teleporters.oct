/**<
    Reusable teleporter entities.

    Author:
        q66 <quaker66@gmail.com>

    License:
        See COPYING.txt.
*/

import core.logger as log
import core.engine.sound
import core.events.signal
import core.entities.svars
import core.entities.ents

var play = sound.play
var connect = signal.connect
var get_by_tag = ents.get_by_tag

from std.math import random as rand

/// Module: teleporters
var M = {}

var Obstacle = ents.Obstacle

/** Object: teleporters.Teleporter
    A regular invisible teleporter. Derives from {{$ents.Obstacle}}. Properties
    can be specified on creation as first two parameters to newent (the rest
    applies to {{$ents.Obstacle}} properties).

    In edit mode, the links from teleporter to destinations are visualized.

    Properties:
        - destination - an integer from 1 to N (0 by default, as in invalid),
          specifies the teleporter destination number (which is a marker
          tagged teledest_N), there can be multiple destinations and the
          teleporter will select one at random.
        - sound_name - name of the sound to play on teleportation, empty
          by default.
*/
M.Teleporter = Obstacle.clone({
    name: "Teleporter",

    __properties: {
        destination: svars::StateInteger(),
        sound_name : svars::StateString()
    },

    __init_svars: func(self, kwargs, nd)
        Obstacle::__init_svars(self, kwargs, [ nd.unpack(2) ])
        self.set_attr("destination", 0, nd[0])
        self.set_attr("sound_name", "", nd[1])
    end,

    __activate: @[not server,func(self, kwargs)
        Obstacle::__activate(self, kwargs)
        connect(self, "collision", self.on_collision)
    end],

    on_collision: func(self, collider)
        var dest = self.get_attr("destination")
        if dest <= 0 do return end
        var dests = get_by_tag("teledest_" ~ dest)
        if dests.len() == 0 do
            log::log(log.ERROR, "No teledest found.")
            return
        end
        dests[rand(0, dests.len())].place_entity(collider)
        var sn = self.get_attr("sound_name")
        if sn != "" do play(sn) end
    end,

    get_attached_next: func(self)
        var dest = self.get_attr("destination")
        if dest <= 0 do return end
        return get_by_tag("teledest_" ~ dest).unpack()
    end
})

ents::register_prototype(M.Teleporter)

return M
